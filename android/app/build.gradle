apply plugin: 'com.android.application'

import groovy.json.JsonSlurper

def packageJsonFile = rootProject.file('../package.json')
def packageJson = null

def packageVersion = '0.0.0'
if (packageJsonFile.exists()) {
    try {
        packageJson = new JsonSlurper().parseText(packageJsonFile.getText('UTF-8'))
        if (packageJson?.version) {
            packageVersion = packageJson.version
        } else {
            println "[nospeak-android] package.json missing version; defaulting to ${packageVersion}"
        }
    } catch (Exception e) {
        println "[nospeak-android] Failed to parse package.json: ${e.message}; defaulting to ${packageVersion}"
    }
} else {
    println "[nospeak-android] package.json not found at ${packageJsonFile}; defaulting to ${packageVersion}"
}

def sanitizedSemver = packageVersion.split(/[\-\+]/)[0]
def semverParts = sanitizedSemver.tokenize('.')

def nospeakVersionCode = packageJson?.versionCode ?: 1
def nospeakVersionName = "v${packageVersion}"

android {
    namespace "com.nospeak.app"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.nospeak.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode nospeakVersionCode
        versionName nospeakVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }

    signingConfigs {
        create("release") {
            def storeFilePath = project.findProperty("RELEASE_STORE_FILE") ?: System.getenv("RELEASE_STORE_FILE")
            def storePasswordValue = project.findProperty("RELEASE_STORE_PASSWORD") ?: System.getenv("RELEASE_STORE_PASSWORD")
            def keyAliasValue = project.findProperty("RELEASE_KEY_ALIAS") ?: System.getenv("RELEASE_KEY_ALIAS")
            def keyPasswordValue = project.findProperty("RELEASE_KEY_PASSWORD") ?: System.getenv("RELEASE_KEY_PASSWORD")

            if (storeFilePath && storePasswordValue && keyAliasValue && keyPasswordValue) {
                storeFile file(storeFilePath)
                storePassword storePasswordValue
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            } else {
                println "[nospeak-android] Release signing config not fully defined; release build will be unsigned."
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def releaseSigningConfig = signingConfigs.findByName("release")
            if (releaseSigningConfig != null && releaseSigningConfig.storeFile != null) {
                signingConfig releaseSigningConfig
            }
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.core:core:$androidxCoreVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
     implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
      implementation project(':capacitor-android')
      implementation "com.squareup.okhttp3:okhttp:4.12.0"
      implementation "androidx.security:security-crypto:1.1.0-alpha06"
      implementation "org.bouncycastle:bcprov-jdk15to18:1.78.1"
      testImplementation "junit:junit:$junitVersion"
     testImplementation "org.json:json:20240303"

    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'
