apply plugin: 'com.android.application'

import groovy.json.JsonSlurper

def packageJsonFile = rootProject.file('../package.json')
def packageJson = null

def packageVersion = '0.0.0'
if (packageJsonFile.exists()) {
    try {
        packageJson = new JsonSlurper().parseText(packageJsonFile.getText('UTF-8'))
        if (packageJson?.version) {
            packageVersion = packageJson.version
        } else {
            println "[nospeak-android] package.json missing version; defaulting to ${packageVersion}"
        }
    } catch (Exception e) {
        println "[nospeak-android] Failed to parse package.json: ${e.message}; defaulting to ${packageVersion}"
    }
} else {
    println "[nospeak-android] package.json not found at ${packageJsonFile}; defaulting to ${packageVersion}"
}

def sanitizedSemver = packageVersion.split(/[\-\+]/)[0]
def semverParts = sanitizedSemver.tokenize('.')

def semverMajor = semverParts.size() > 0 ? (semverParts[0] as int) : 0
def semverMinor = semverParts.size() > 1 ? (semverParts[1] as int) : 0
def semverPatch = semverParts.size() > 2 ? (semverParts[2] as int) : 0

def nospeakVersionCode = (semverMajor * 1000000) + (semverMinor * 1000) + semverPatch
def nospeakVersionName = "v${packageVersion}"

android {
    namespace "com.nospeak.app"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.nospeak.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode nospeakVersionCode
        versionName nospeakVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        release {
            def storeFilePath = project.findProperty("RELEASE_STORE_FILE") ?: System.getenv("RELEASE_STORE_FILE")
            def storePasswordValue = project.findProperty("RELEASE_STORE_PASSWORD") ?: System.getenv("RELEASE_STORE_PASSWORD")
            def keyAliasValue = project.findProperty("RELEASE_KEY_ALIAS") ?: System.getenv("RELEASE_KEY_ALIAS")
            def keyPasswordValue = project.findProperty("RELEASE_KEY_PASSWORD") ?: System.getenv("RELEASE_KEY_PASSWORD")

            if (storeFilePath && storePasswordValue && keyAliasValue && keyPasswordValue) {
                storeFile file(storeFilePath)
                storePassword storePasswordValue
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            } else {
                println "[nospeak-android] Release signing config not fully defined; release build will be unsigned."
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
     implementation project(':capacitor-android')
     implementation "com.squareup.okhttp3:okhttp:4.12.0"
     testImplementation "junit:junit:$junitVersion"

    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
